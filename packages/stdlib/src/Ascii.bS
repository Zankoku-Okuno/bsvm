; Library that defines constants and procedures to manipulate ASCII text.

.def Ascii.space 20h
.def Ascii.tab 09h
.def Ascii.nl 0Ah

.def Ascii.zero 30h
.def Ascii.nine 39h
.def Ascii.A 41h
.def Ascii.Z 5Ah
.def Ascii.a 61h
.def Ascii.z 7Ah

.def Ascii.comma 2Ch
.def Ascii.semicolon 3Bh

; Return True iff the input char is alphabetic r/[A-Za-z]/
;
; char: byte
.func Ascii.isAlpha, char
  .reg c, t1
  ;;; when char < 'A' { return False }
  bl c, char, $Ascii.A
  cjmp c, @no
  ;;; when char <= 'Z' { return True }
  ble c, char, $Ascii.Z
  cjmp c, @yes
  ;;; when char < 'a' { return False }
  bl c, char, $Ascii.a
  cjmp c, @no
  ;;; when char <= 'z' { return True }
  ble c, char, $Ascii.z
  cjmp c, @yes
  ;;; return False
@no:
  mov t1, $False
  ret t1
@yes:
  mov t1, $True
  ret t1

; Return True iff the input char is alphanumeric r/[A-Za-z0-9]/
;
; char: byte
.func Ascii.isAlphaNum, char
  .reg c, t1
  ;;; when char < '0' { return False }
  bl c, char, $Ascii.zero
  cjmp c, @no
  ;;; when char <= '9' { return True }
  ble c, char, $Ascii.nine
  cjmp c, @yes
  ;;; return Ascii.isAlpha(char)
  jar &Ascii.isAlpha, char
@no:
  mov t1, $False
  ret t1
@yes:
  mov t1, $True
  ret t1

; Return True iff the char is present in the passes asciiz string
;
; char: byte
; set: *asciiz
.func Ascii.in, char, set
  .reg x
  .reg c
  ;;; c = False
  mov c, $False
  ;;; while (x = *set++) != 0 {
  @loop:
  ldb x, set
  zjmp x, @loop.done
  add set, 1
    ;;; when c = (char == x) { return c } // which at this point is true
    eq c, char, x
    zjmp c, @loop
  ;;; }
  @loop.done:
  ;;; return c // which is either true if we broke out
  ;;;          // or false if we exited the loop normally
  ret c